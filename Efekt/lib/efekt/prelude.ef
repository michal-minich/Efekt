let not = fn a { if a then false else true }
let or = fn a, b { if a then true else { if b then true else false } }
let and = fn a, b { if a then { if b then true else false } else false }
let >= = fn a, b { not(a < b) }
let <= = fn a, b { a < b or a == b }
let > = fn a, b { not(a < b) and not(a == b) }
let printLine = fn a { print(a) print('\n') }
let id = fn a { a }

let from = fn a {
    new {
        var arr = a
        var position = 0 - 1
        let reset = fn { position = 0 }
        let current = fn { at(arr, position) }
        let moveForward = fn {
            position = position + 1
            return position < count(arr)
        }
    }
}

let where = fn i, p {
    new {
        var iterator = i
        var predicate = p
        let current = fn { i.current() }
        let moveForward = fn {
            var ok = false
            loop {
                ok = i.moveForward()
                if not(ok) then
                    return false
                if predicate(i.current()) then
                    return true
            }
        }
    }
}

let forEach = fn i, f {
    loop {
        if not(i.moveForward()) then
            return
        f(i.current())
    }
}

let calculateCount = fn i {
    var c = 0
    loop {
        if not(i.moveForward()) then
            break
        c = c + 1
    }
    return c
}

let assert = fn condition {
    if not(condition) then
        throw "assert is false"
}

let for = fn from, to, do {
    assert(from <= to)
    var i = from
    loop {
        do(i)
        if i == to then
            break
        i = i + 1
    }
}